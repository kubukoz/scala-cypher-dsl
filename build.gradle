plugins {
    id 'maven'
    id 'signing'
    id 'scala'
    id 'idea'
    id "com.github.maiflai.scalatest" version "0.19"
    id "com.adtran.scala-multiversion-plugin" version "1.0.35"
}

group 'me.manishkatoch'
sourceCompatibility = 1.8

ext {
    mavenUrl = project.properties.getOrDefault('maven2_url', System.getenv("MVN_URL"))
    snapshot_url = project.properties.getOrDefault('snapshot_url', System.getenv("MVN_SNAPSHOT_URL"))
    mavenRepositoryUsername = project.properties.getOrDefault('mavenRepositoryUsername', System.getenv("MVN_USERNAME"))
    mavenRepositoryPassword = project.properties.getOrDefault('mavenRepositoryPassword', System.getenv("MVN_PASSWORD"))
    build_version = project.properties.getOrDefault('buildVersion', System.getenv("BUILD_VERSION"))
}

version build_version

repositories {
    mavenCentral()
}

def isRunningOnTravis = System.getenv("CI") == "true"

dependencies {
    implementation group: 'org.scala-lang', name: 'scala-library', version: '%scala-version%'
    implementation group: 'org.scala-lang', name: 'scala-compiler', version: '%scala-version%'
    implementation group: 'org.scala-lang', name: 'scala-reflect', version: '%scala-version%'
    implementation group: 'com.chuusai', name: 'shapeless_%%', version: '2.3.3'
    testImplementation group: 'org.scalatest', name: 'scalatest_%%', version: '3.0.1'
    testRuntime 'org.pegdown:pegdown:1.4.2'
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign }) {
        allprojects { ext."signing.keyId" = System.getenv('GPG_KEY_ID') }
        allprojects {
            if (isRunningOnTravis) {
                ext."signing.secretKeyRingFile" = file("./secure.gpg")
            } else {
                ext."signing.secretKeyRingFile" = "./secure.gpg"
            }
        }

        allprojects { 
        ext."signing.password" = System.getenv('GPG_PASSPHRASE')
        }
    }
    if (taskGraph.allTasks.any { it.name == 'build' || it.name == 'assemble' }) {
        tasks.findAll {
            it.name == 'signArchives' || it.name == 'signDocsJar' || it.name == 'signTestJar'
        }.each { task ->
            task.enabled = false
        }
    }
}

signing {
    sign configurations.archives
}

task scaladocJar(type: Jar, dependsOn: scaladoc) {
    classifier = 'javadoc'
    from "${project.buildDir}/docs/scaladoc"
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives scaladocJar
    archives sourceJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }
            repository(url: mavenUrl) {
                authentication(userName: mavenRepositoryUsername, password: mavenRepositoryPassword)
            }
            snapshotRepository(url: snapshot_url) {
                authentication(userName: mavenRepositoryUsername, password: mavenRepositoryPassword)
            }
            pom.project {
                name 'Scala Cypher DSL'
                packaging 'jar'
                description 'A type safe DSL for writing Cypher Query Language in Scala.'
                url 'https://github.com/manishkkatoch/scala-cypher-dsl'
                scm {
                    connection 'scm:git:https://github.com/manishkkatoch/scala-cypher-dsl.git'
                    developerConnection 'scm:git:https://github.com/manishkkatoch/scala-cypher-dsl.git'
                    url 'https://github.com/manishkkatoch/scala-cypher-dsl'
                }
                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/manishkkatoch/scala-cypher-dsl/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id 'manishkatoch'
                        name 'Manish Katoch'
                    }
                }
            }
        }
    }
}
